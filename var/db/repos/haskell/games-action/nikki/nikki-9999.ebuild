# Copyright 1999-2018 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6

# ebuild generated by hackport 0.3.3.9999

CABAL_FEATURES="test-suite"
inherit git-r3 eutils flag-o-matic haskell-cabal

DESCRIPTION="Nikki and the robots game"
HOMEPAGE="https://github.com/nikki-and-the-robots/nikki"
EGIT_REPO_URI="https://github.com/nikki-and-the-robots/nikki.git"

LICENSE="CC-BY-SA-3.0"
SLOT="0"
IUSE="devel"

RDEPEND="dev-qt/qtcore:4=
	dev-qt/qtgui:4=
	dev-qt/qtopengl:4=
"
DEPEND="${RDEPEND}
	dev-haskell/aeson
	dev-haskell/bifunctors
	dev-haskell/binary
	>=dev-haskell/binary-communicator-1.0.2
	>=dev-haskell/cabal-1.16.0
	>=dev-haskell/clocked-0.4.1 <dev-haskell/clocked-0.4.2
	>=dev-haskell/cmdargs-0.6.6
	>=dev-haskell/crypto-pubkey-types-0.1 <dev-haskell/crypto-pubkey-types-0.5
	>=dev-haskell/data-accessor-0.2.1.7
	>=dev-haskell/data-accessor-mtl-0.2.0.2
	>=dev-haskell/email-validate-1.0
	>=dev-haskell/findbin-0.0.5
	>=dev-haskell/hipmunk-5.2.0.6
	>=dev-haskell/http-4000.2
	>=dev-haskell/libzip-0.2.0.4
	>=dev-haskell/monadcatchio-transformers-0.2.2.2
	>=dev-haskell/mtl-2 <dev-haskell/mtl-3
	>=dev-haskell/network-2 <dev-haskell/network-3
	>=dev-haskell/parsec-3 <dev-haskell/parsec-4
	dev-haskell/random
	>=dev-haskell/rsa-1.2.1
	dev-haskell/safe
	>=dev-haskell/sfml-audio-0.7.1 <dev-haskell/sfml-audio-0.8
	dev-haskell/statevar
	>=dev-haskell/stickykeyshotkey-0.1 <dev-haskell/stickykeyshotkey-0.2
	>=dev-haskell/strict-0.3.2
	dev-haskell/string-conversions
	>=dev-haskell/template-0.2 <dev-haskell/template-0.3
	>=dev-haskell/temporary-1.1.1
	dev-haskell/text
	dev-haskell/transformers
	>=dev-haskell/uniplate-1.6
	>=dev-haskell/utf8-string-0.3
	>=dev-haskell/vector-0.10
	>=dev-lang/ghc-7.6.1
	>=dev-haskell/hashable-1.1
	<dev-haskell/hashable-1.3
	>dev-haskell/mtl-2.1
	>=dev-haskell/transformers-0.3
	dev-util/cmake
"

S="${WORKDIR}/${P}/src"

PATCHES=("${FILESDIR}"/${P}-binary-hack.patch)

src_prepare() {
	default

	cabal_chdeps \
		'-optl-s' ' ' \
		'HTTP ==4000.2.*' 'HTTP >=4000.2' \
		'vector ==0.10.*' 'vector >=0.10'
}

src_configure() {
	# FIXME: use proper eclass to set libs dir
	append-ldflags -L/usr/$(get_libdir)/qt4

	# inlined version of build-qtwrapper.sh
	mkdir -p cpp/dist || die
	pushd cpp/dist || die
	PATH="${EPREFIX}/usr/$(get_libdir)/qt4/bin:$PATH" cmake .. || die
	emake VERBOSE=1
	popd || die

	haskell-cabal_src_configure \
		$(cabal_flag devel devel)
}

src_install() {
	haskell-cabal_src_install

	# FIXME: it's sharedir is fun :]
	insinto /usr/bin
	doins -r ../data/
}
